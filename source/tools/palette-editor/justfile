import "../../common/common.justfile"

#==============================================================================
# default
#==============================================================================

[private]
default:
    @just --list --unsorted

#==============================================================================
# ensure
#==============================================================================

ensure:
    @just ensure-vscode-directory

#==============================================================================
# build
#==============================================================================

build: ensure build-only

[private]
build-only:
    mkdir -p dist
    cp -f src/dist/* dist/
    deno bundle \
      --unstable-raw-imports \
      --platform=browser \
      --sourcemap=inline \
      --output dist/main.bundle.js \
      src/main.tsx
    date > dist/build.timestamp

#==============================================================================
# dev
#==============================================================================

dev: ensure
    mprocs \
        --proc-list-title "palette-editor 9000" \
        --names "client,server" \
        "just dev-client" \
        "just dev-server"

dev-client:
    watchexec \
        --watch src \
        --watch "${REPO_ROOT}/source/modules/raiment-core/src" \
        --watch "${REPO_ROOT}/source/modules/raiment-ui/src" \
        --exts ts,tsx,html,css,png,yaml,json \
        "just build-only && sleep 1"


# Unfortunately can't just call "just dev" in the server directory
# as that requires changing the working directory, but we want the
# working to remain relative to this project.

# Starts the development server with live reloading
dev-server:
    watchexec \
        --restart \
        --watch "${REPO_ROOT}/source/tools/raiment-dev-server/src" \
        --watch "${REPO_ROOT}/source/modules/raiment-core/src" \
        --watch "${REPO_ROOT}/source/modules/raiment-ui/src" \
        --exts ts,tsx,html,css,png,yaml,json \
        "just run-server"

#==============================================================================
# run
#==============================================================================

run-server:
    deno run -A "${REPO_ROOT}/source/tools/raiment-dev-server/main.ts" \
        --port 7050 \
        --title "palette-editor"
